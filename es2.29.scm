(define (make-mobile left right)
  (cons left right))
(define (left-branch mobile)
  (car mobile))
(define (right-branch mobile)
  (cdr mobile))
(define (make-branch length structure)
  (cons length structure))
(define (branch-length branch)
  (car branch))
(define (branch-structure branch)
  (cdr branch))
(define (is-mobile? maybe-mobile)
  (pair? maybe-mobile))
(define (total-weight-branch branch)
  (let ((structure (branch-structure branch)))
    (if (is-mobile? structure)
        (total-weight structure)
        structure)))
(define (total-weight mobile)
  (+ (total-weight-branch (left-branch mobile))
     (total-weight-branch (right-branch mobile))))
(define (torque branch)
  (* (branch-length branch)
     (total-weight-branch branch)))
(define (balanced-branch? branch)
  (let ((structure (branch-structure branch)))
    (if (is-mobile? structure)
        (balanced? structure)
        #t)))
(define (balanced? mobile)
  (let ((left (left-branch mobile))
        (right (right-branch mobile)))
    (and (= (torque left)
            (torque right))
         (balanced-branch? left)
         (balanced-branch? right))))

(define my-mobile (make-mobile (make-branch 10 5)
                               (make-branch 10 (make-mobile (make-branch 2 3)
                                                            (make-branch 2 2)))))

(define balanced-mobile (make-mobile (make-branch 10 (make-mobile (make-branch 2 2)
                                                                  (make-branch 2 2)))
                                     (make-branch 10 (make-mobile (make-branch 2 2)
                                                                  (make-branch 2 2)))))

